# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../batterymgr.gni")

copy_taihe_idl("copy_battery_manager") {
  sources = [ "idl/ohos.batteryInfo.taihe" ]
}

subsystem_name = "powermgr"
part_name = "battery_manager"
taihe_generated_file_path_batteryInfo = "$taihe_file_path/out/$subsystem_name/$part_name/batteryInfo"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_batteryInfo}"
  deps = [ ":copy_battery_manager" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.batteryInfo.ani.cpp",
    "$taihe_generated_file_path/src/ohos.batteryInfo.abi.c",
  ]
}

config("batterynapi_private_config") {
  include_dirs = [ "include" ]
}

taihe_shared_library("battery_manager_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "${taihe_generated_file_path_batteryInfo}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${battery_utils}:utils_config",
    ":batterynapi_private_config",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.batteryInfo.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${battery_inner_api}:batterysrv_client",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
}

generate_static_abc("batteryInfo_abc") {
  base_url = "$taihe_generated_file_path_batteryInfo"
  files = [ "$taihe_generated_file_path_batteryInfo/@ohos.batteryInfo.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/batteryInfo_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("batteryInfo_abc_etc") {
  source = "$target_out_dir/batteryInfo_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":batteryInfo_abc" ]
}