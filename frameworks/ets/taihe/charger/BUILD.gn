# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../batterymgr.gni")

copy_taihe_idl("copy_battery_manager_charger") {
  sources = [ "idl/ohos.charger.taihe" ]
}

subsystem_name = "powermgr"
part_name = "battery_manager"
taihe_generated_file_path_charger = "$taihe_file_path/out/$subsystem_name/$part_name/charger"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_charger}"
  deps = [ ":copy_battery_manager_charger" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.charger.ani.cpp",
    "$taihe_generated_file_path/src/ohos.charger.abi.c",
  ]
}

taihe_shared_library("battery_manager_charger_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "${taihe_generated_file_path_charger}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${battery_utils}:utils_config",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.charger.impl.cpp",
  ]
  deps = [
    ":run_taihe",
  ]
}

generate_static_abc("charger") {
  base_url = "$taihe_generated_file_path_charger"
  files = [ "$taihe_generated_file_path_charger/@ohos.charger.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/charger.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("charger_abc_etc") {
  source = "$target_out_dir/charger.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":charger" ]
}